h1. Programatically Editing app.config files

[[Source File on GitHub|https://github.com/basho/riak_function_contrib/blob/master/other/erlang/app_config_edit.erl]] 

These Erlang functions allow you to read or edit values in the Riak app.config file.

There are two exported functions:
app_config_edit:get_config_value(app config, section, name)
app_config_edit:update_config_value(app config, section, name, new value)

```erlang
-module(app_config_edit).
-export([get_config_value/3, update_config_value/4, example/1]).

example(FilePath) -> 
	{ok,[AppConfig]} = file:consult(FilePath),
	[Http] = get_config_value(AppConfig, riak_core, http),
	io:format("HTTP Address: ~p~n", [Http]),
	Updated = update_config_value(AppConfig, riak_core, http, [{"0.0.0.0", 8080}]),
	io:format("Original Values:~n~p~n", [AppConfig]),
	io:format("Updated Values:~n~p~n", [Updated]).	

update_config_value([], _, _, _) ->
	[];
update_config_value([Section | Rest], SectionName, Name, NewValue) ->
	case Section of
		{SectionName, Values} ->
			[{SectionName, update_config_value_in_section(Values, Name, NewValue)} | update_config_value(Rest, SectionName, Name, NewValue)];
		Other ->
			[Other | update_config_value(Rest, SectionName, Name, NewValue)]
	end.

update_config_value_in_section([], _, _) ->
	[];
update_config_value_in_section([Value | Rest], Name, NewValue) ->
	case Value of 
		{Name, _V} ->
			[{Name, NewValue} | update_config_value_in_section(Rest, Name, NewValue)];
		Other ->
			[Other | update_config_value_in_section(Rest, Name, NewValue)]
	end.
	

get_config_value([], _, _) ->
	not_found;
get_config_value([Section | Rest], SectionName, Name) ->
	case Section of
		{SectionName, Values} ->
			get_config_value_in_section(Values, Name);
		_ ->
			get_config_value(Rest, SectionName, Name)
	end.
	
get_config_value_in_section([], _) ->
	not_found;
get_config_value_in_section([Value | Rest], Name) ->
	case Value of 
		{Name, V} ->
			V;
		_ ->
			get_config_value_in_section(Rest, Name)
	end.
```

h3. Ideas for using these function

These functions were originally written to help facilitate automated deployment of a Riak cluster. You could write a
program that uses these functions to configure Riak appropriately for your environment and avoid errors that
can occur by doing these tasks manually.
